#!/usr/bin/perl
#

use 5.008;
use lib (split(/:/, $ENV{GITPERLLIB}));
use strict;
use warnings;
use Git::Packet;
use LWP::UserAgent;
use HTTP::Request::Common;

packet_initialize("git-read-object", 1);

packet_read_and_check_capabilities("get", "put", "have");
packet_write_capabilities("get", "put", "have");

my $http_url = $ENV{HTTPD_URL};

while (1) {
	my ($res, $command) = packet_txt_read();

	if ( $res == -1 ) {
		exit 0;
	}

	$command =~ s/^command=//;

	if ( $command eq "init" ) {
		packet_bin_read();

		packet_txt_write("status=success");
		packet_flush();
	} elsif ( $command eq "have" ) {
		# read the flush after the command
		packet_bin_read();

		my $have_url = $http_url . "/list/";

		my $userAgent = LWP::UserAgent->new();
		my $response = $userAgent->get( $have_url );

		if ($response->is_error) {
			packet_bin_write("");
			packet_flush();
			packet_txt_write("status=failure");
		} else {
			packet_bin_write($response->content);
			packet_flush();
			packet_txt_write("status=success");
		}
		packet_flush();
	} elsif ( $command eq "get" ) {
		my ($sha1) = packet_txt_read() =~ /^sha1=([0-9a-f]{40})$/;
		packet_bin_read();

		my $get_url = $http_url . "/list/?sha1=" . $sha1;

		my $userAgent = LWP::UserAgent->new();

		my $response = $userAgent->get( $get_url );

		if ($response->is_error) {
			packet_txt_write("size=0");
			packet_txt_write("kind=none");	    
			packet_txt_write("status=notfound");
		} else {
			packet_txt_write("size=" . length($response->content));
			packet_txt_write("kind=blob");
			packet_bin_write($response->content);
			packet_flush();
			packet_txt_write("status=success");
		}

		packet_flush();
	} elsif ( $command eq "put" ) {
		my ($sha1) = packet_txt_read() =~ /^sha1=([0-9a-f]{40})$/;
		my ($size) = packet_txt_read() =~ /^size=([0-9]+)$/;
		my ($kind) = packet_txt_read() =~ /^kind=(\w+)$/;

		packet_bin_read();

		# We must read the content we are sent and send it to the right url
		my ($res, $buf) = packet_bin_read();
		die "bad packet_bin_read res ($res)" unless ($res eq 0);
		( packet_bin_read() eq ( 1, "" ) ) || die "bad send end";		

		my $upload_url = $http_url . "/upload/?sha1=" . $sha1 . "&size=" . $size . "&type=blob";

		my $userAgent = LWP::UserAgent->new();
		my $request = POST $upload_url, Content_Type => 'multipart/form-data', Content => $buf;

		my $response = $userAgent->request($request);

		if ($response->is_error) {
			packet_txt_write("status=failure");
		} else {
			packet_txt_write("status=success");
		}
		packet_flush();
	} else {
		die "bad command '$command'";
	}
}
